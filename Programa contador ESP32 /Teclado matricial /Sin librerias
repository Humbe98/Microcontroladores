const byte FILAS = 4, COLUMNAS = 4;
char teclas[FILAS][COLUMNAS] = {
  {'1', '2', '3', 'A'},
  {'4', '5', '6', 'B'},
  {'7', '8', '9', 'C'},
  {'*', '0', '#', 'D'}
};
byte pinesFilas[FILAS] = {18, 19, 21, 22}; //R1, R2, R3, R4
byte pinesColumnas[COLUMNAS] = {5, 4, 16, 17};//C1, C2, C3, C4

void setup() {
  Serial.begin(9600);  // Inicializamos el monitor serial
  for (int i = 0; i < FILAS; i++) pinMode(pinesFilas[i], OUTPUT);
  for (int i = 0; i < COLUMNAS; i++) pinMode(pinesColumnas[i], INPUT_PULLUP);
  Serial.println("Empieza a teclear");
}

void loop() {
  char tecla = Tecla();  // Ver si se presionó una tecla
  if (tecla != '\0') {  // Si se presionó una tecla
    Serial.print("Tecla: ");  // Mostramos el texto en el monitor serial
    Serial.println(tecla);  // Mostramos la tecla presionada
    delay(100);  // Evitamos múltiples lecturas de la misma tecla
  }
}

// Detectar tecla presionada
char Tecla() {
  for (int i = 0; i < FILAS; i++) {
    digitalWrite(pinesFilas[i], LOW);  // Activamos una fila
    for (int j = 0; j < COLUMNAS; j++) {
      if (digitalRead(pinesColumnas[j]) == LOW) {  // Si se presiona una tecla
        digitalWrite(pinesFilas[i], HIGH);         // Desactivamos la fila
        return teclas[i][j];  // Devolvemos la tecla presionada
      }
    }
    digitalWrite(pinesFilas[i], HIGH);  // Desactivamos la fila
  }
  return '\0';  // No se ha presionado ninguna tecla
}
